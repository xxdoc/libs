Attribute VB_Name = "Module1"
Option Explicit

Global bitFlags(31) As Long
Private Declare Sub CopyMemory Lib "kernel32" Alias "RtlMoveMemory" (pDest As Any, pSrc As Any, ByVal ByteLen As Long)
Private Declare Function htonl Lib "ws2_32.dll" (ByVal hostlong As Long) As Long

'I dont really want a module rather have self contained in the classes but hexdump is used in main form and structfield
'will tidy/rescope latter..

Sub CurToLongs(c As Currency, ByRef hi As Long, lo As Long)
    
    CopyMemory ByVal VarPtr(hi), ByVal VarPtr(c), 4
    CopyMemory ByVal VarPtr(lo), ByVal VarPtr(c) + 4, 4
    hi = htonl(hi)
    lo = htonl(lo)
    
End Sub

Function CurToHex(cur) As String
    Dim hi As Long, lo As Long
    CurToLongs CCur(cur), hi, lo
    CurToHex = Right("00000000" & Hex(hi), 8) & Right("00000000" & Hex(lo), 8)
End Function

Sub push(ary, Value) 'this modifies parent ary object
    On Error GoTo init
    Dim x
    x = UBound(ary) '<-throws Error If Not initalized
    ReDim Preserve ary(UBound(ary) + 1)
    ary(UBound(ary)) = Value
    Exit Sub
init:     ReDim ary(0): ary(0) = Value
End Sub

Function HexDumpB(b() As Byte, Optional hexOnly = 0) As String
    HexDumpB = HexDump(StrConv(b, vbUnicode, &H409), hexOnly)
End Function

Function HexDump(ByVal str, Optional hexOnly = 0) As String
    Dim s() As String, chars As String, tmp As String, i, tt, x, h
    On Error Resume Next
    Dim ary() As Byte
    Dim offset As Long
    
    offset = 0
    str = " " & str
    ary = StrConv(str, vbFromUnicode, &H409)
    
    chars = "   "
    For i = 1 To UBound(ary)
        tt = Hex(ary(i))
        If Len(tt) = 1 Then tt = "0" & tt
        tmp = tmp & tt & " "
        x = ary(i)
        'chars = chars & IIf((x > 32 And x < 127) Or x > 191, Chr(x), ".") 'x > 191 causes \x0 problems on non us systems... asc(chr(x)) = 0
        chars = chars & IIf((x > 32 And x < 127), Chr(x), ".")
        If i > 1 And i Mod 16 = 0 Then
            h = Hex(offset)
            While Len(h) < 6: h = "0" & h: Wend
            If hexOnly = 0 Then
                push s, h & "   " & tmp & chars
            Else
                push s, tmp
            End If
            offset = offset + 16
            tmp = Empty
            chars = "   "
        End If
    Next
    'if read length was not mod 16=0 then
    'we have part of line to account for
    If tmp <> Empty Then
        If hexOnly = 0 Then
            h = Hex(offset)
            While Len(h) < 6: h = "0" & h: Wend
            h = h & "   " & tmp
            While Len(h) <= 56: h = h & " ": Wend
            push s, h & chars
        Else
            push s, tmp
        End If
    End If
    
    HexDump = Join(s, vbCrLf)
    
    If hexOnly <> 0 Then
        HexDump = Replace(HexDump, " ", "")
        HexDump = Replace(HexDump, vbCrLf, "")
    End If
    
End Function

